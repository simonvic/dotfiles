###########################################
#	Made by
#       _                              _
#      (_)                            (_)
#  ___  _  ____    ___   ____   _   _  _   ____
# /___)| ||    \  / _ \ |  _ \ | | | || | / ___)
#|___ || || | | || |_| || | | | \ V / | |( (___
#(___/ |_||_|_|_| \___/ |_| |_|  \_/  |_| \____)

#	Check updates and give a look at my dotfiles here:
#		https://github.com/simonvic/dotfiles

###########################################


# The minimum characters count to be left available before going to new line
# useful when showing full directory path
ZSH_S_MINIMUM_FREE_INPUT_CHARS=50

# Colors can be defined with a string (e.g. red, blue, yellow)
# or with a numeric value (e.g. 000, 001, 002)
# Use spectrum_ls or spectrum_bls for a list of available numeric values
ZSH_S_COLOR_ACCENT='red'
ZSH_S_COLOR_DIRECTORY='red'
ZSH_S_COLOR_USERNAME='red'
ZSH_S_COLOR_HOST='red'
ZSH_S_COLOR_PROMPT_CHAR='red'
ZSH_S_COLOR_DATETIME='242'
ZSH_S_COLOR_DECORATIONS='245'
ZSH_S_COLOR_RETURN_TRUE='115'
ZSH_S_COLOR_RETURN_FALSE='136'
ZSH_S_COLOR_VCS_BRANCH='229'

# %0/       Returns the entire directory structure.
# %1/       Returns the current directory.
# %2/       Returns the current directory and its parent directory.
# %number/  In general, %number/ returns the directory structure to number levels deep.
# %~        Show "~" when the current directory is the home
# %number~  Same as above but show "~" when the current directory is the home
ZSH_S_DIRECTORY_FORMAT="%~"

# Char to be shown at the start of the command
ZSH_S_PROMPT_CHAR_ROOT='#'
ZSH_S_PROMPT_CHAR_USER=''

# strftime format
ZSH_S_DATETIME_FORMAT="%H:%M:%S"

# Use '\e' escapes and wrap it inside a glitch block %{ %} with 0 width
ZSH_S_BLINK="%0{$(echo -e '\e[5m')%}"
ZSH_S_ITALIC="%0{$(echo -e '\e[3m')%}"
ZSH_S_RESET="%0{$(echo -e '\e[0m')%}"

# VCS info
ZSH_S_VCS_FORMAT="%F{$ZSH_S_COLOR_DECORATIONS}¦%f %u%c%F{$ZSH_S_COLOR_VCS_BRANCH}%b%f%%b$ZSH_S_RESET "
ZSH_S_VCS_ACTIONFORMAT="$ZSH_S_VCS_FORMAT%F{$ZSH_S_COLOR_DECORATIONS} ¦ %f %S%a%%s "
ZSH_S_VCS_STAGEDSTR="$ZSH_S_ITALIC"
ZSH_S_VCS_UNSTAGEDSTR="%B"

################################################################################

zstyle ':vcs_info:*' formats "$ZSH_S_VCS_FORMAT"
zstyle ':vcs_info:*' actionformats "$ZSH_S_VCS_ACTIONFORMAT"
zstyle ':vcs_info:*' stagedstr "$ZSH_S_VCS_STAGEDSTR"
zstyle ':vcs_info:*' unstagedstr "$ZSH_S_VCS_UNSTAGEDSTR"

# Prompt
PS1=""

# line start
PS1+="%F{$ZSH_S_COLOR_ACCENT}┃%f"

# SSH info
[[ -n $SSH_CONNECTION ]] && \
	PS1+=" %F{$ZSH_S_COLOR_USERNAME}%n%F{$ZSH_S_COLOR_DECORATIONS}@%F{$ZSH_S_COLOR_HOST}%M%f"

# directory
PS1+=" %F{$ZSH_S_COLOR_DIRECTORY}$ZSH_S_DIRECTORY_FORMAT%f "

# VCS info
PS1+='${vcs_info_msg_0_}'

# New line (if necessary)
# If ZSH_S_MINIMUM_FREE_INPUT_CHARS are not available on the line, a new line
# will begin and ZSH_S_PROMPT_NEWLINE_START will be printed
PS1+=$(echo "%-$ZSH_S_MINIMUM_FREE_INPUT_CHARS(l..\n%F{$ZSH_S_COLOR_ACCENT}┣ %f)")

# Prompt char
PS1+="%F{$ZSH_S_COLOR_PROMPT_CHAR}%(!,$ZSH_S_PROMPT_CHAR_ROOT,$ZSH_S_PROMPT_CHAR_USER)%f "


# Rear prompt
RPS1=""

# Last process return code
RPS1+="%F{$ZSH_S_COLOR_DECORATIONS}R-%(?,%F{$ZSH_S_COLOR_RETURN_TRUE},%F{$ZSH_S_COLOR_RETURN_FALSE})%?%f"

# Date time
RPS1+=" %F{$ZSH_S_COLOR_DECORATIONS}¦ %F{$ZSH_S_COLOR_DATETIME}%D{$ZSH_S_DATETIME_FORMAT}%f"

# Subshell level (only shown when SHLVL >= 2)
RPS1+="%2(L, %F{$ZSH_S_COLOR_DECORATIONS}¦ 󰆏 $SHLVL,)"

# clean up
unset -v \
	ZSH_S_MINIMUM_FREE_INPUT_CHARS \
	ZSH_S_COLOR_ACCENT \
	ZSH_S_COLOR_DIRECTORY \
	ZSH_S_COLOR_USERNAME \
	ZSH_S_COLOR_HOST \
	ZSH_S_COLOR_PROMPT_CHAR \
	ZSH_S_COLOR_DATETIME \
	ZSH_S_COLOR_DECORATIONS \
	ZSH_S_COLOR_RETURN_TRUE \
	ZSH_S_COLOR_RETURN_FALSE \
	ZSH_S_COLOR_VCS_BRANCH \
	ZSH_S_DIRECTORY_FORMAT \
	ZSH_S_PROMPT_CHAR_ROOT \
	ZSH_S_PROMPT_CHAR_USER \
	ZSH_S_DATETIME_FORMAT \
	ZSH_S_ITALIC \
	ZSH_S_BLINK \
	ZSH_S_RESET \
	ZSH_S_VCS_FORMAT \
	ZSH_S_VCS_ACTIONFORMAT \
	ZSH_S_VCS_STAGEDSTR \
	ZSH_S_VCS_UNSTAGEDSTR

